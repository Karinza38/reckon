plugins {
  id 'groovy'
  id 'java-gradle-plugin'
}

sourceCompatibility = 8

repositories {
  jcenter()
}

dependencies {
  // gradle
  compile gradleApi()

  // reckon
  compile project(':reckon-core')

  // git
  compile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
  compile "org.ajoberstar:grgit:$grgitVersion"
  compatTestCompile "org.ajoberstar:grgit:$grgitVersion"

  // util
  compile 'com.google.guava:guava:latest.release'
  compile 'com.github.zafarkhaja:java-semver:latest.release'

  // testing
  compatTestCompile gradleTestKit()
  compatTestCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
  compatTestCompile 'org.codehaus.groovy:groovy-all:2.4.15'
  compatTestCompile 'junit:junit:latest.release'
}

stutter {
  sparse = true
  includeActiveRc = true

  java(8) {
    compatibleRange '3.0'
  }
  java(9) {
    compatibleRange '4.2.1'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/reckon'
  vcsUrl = 'https://github.com/ajoberstar/reckon.git'
  description = 'Infer a project\'s version from your Git repository'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.reckon'
      displayName = 'Reckon Plugin'
      tags = ['semver', 'git', 'version', 'versioning']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
