plugins {
  id 'groovy'
  id 'java-gradle-plugin'
}

sourceCompatibility = '1.8'

repositories {
  jcenter()
}

dependencies {
  // gradle
  compile gradleApi()

  // reckon
  compile project(':reckon-core')

  // git
  compile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
  compile "org.ajoberstar:grgit:$grgitVersion"
  compatTestCompile "org.ajoberstar:grgit:$grgitVersion"

  // util
  compile 'com.google.guava:guava:[21.0,)'
  compile 'com.github.zafarkhaja:java-semver:[0.9.0,)'

  // testing
  compatTestCompile gradleTestKit()
  compatTestCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
  compatTestCompile 'org.codehaus.groovy:groovy-all:2.4.15'
  compatTestCompile 'junit:junit:4.12'
}

stutter {
  includeActiveRc = true

  java(8) {
    compatibleRange '3.0'
  }
  java(9) {
    compatibleRange '4.2.1'
  }
  java(10) {
    compatibleRange '4.1'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/reckon'
  vcsUrl = 'https://github.com/ajoberstar/reckon.git'
  description = 'Infer a project\'s version from your Git repository'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.reckon'
      displayName = 'Reckon Plugin'
      tags = ['semver', 'git']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
